Functors
========

Functors are morphisms in a *large* category called ***Cat***, which is a category of [small categories](https://en.wikipedia.org/wiki/Category_(mathematics)#Small_and_large_categories).

For a functor to actually be a morphism between categories it must completely map one category into another retaining the original structure, which in practice means that the functor must map all the objects and morphisms from one category into another preserving composition and identity.

![Functor](https://user-images.githubusercontent.com/7043153/121792030-406c5b80-cbbe-11eb-9e8d-becc0e31b34c.png)


Endofunctors
------------
Endofunctors are functors that map the objects and morphisms of one category into objects and morphisms of the same category. This kind of functor is the one implemented by Haskell, where objects can only be types from the Hask category and any mapped objects will still be objects of the Hask category.

Functors in Programming
-----------------------
Haskell abstracts functors using a *typeclass*:
```haskell
class Functor f where
    fmap :: (a -> b) -> f a -> f b
```

Here `fmap` is used to preserve/map identity and composition, while the type constructor itself preserves/maps objects.

### Typeclasses
*Typeclasses* are a mechanism to define families of types that support the same interface, they're similar to Java interfaces in the sense that their definition is only a specification of supported methods; but their implementation is very different, typeclasses are implemented after the implementing type is defined.

### Functor Laws
- Identity must be preserved: `fmap id = id`
- Composition must be preserved: `fmap (f . g) = fmap f . fmap g`

### The Maybe Functor
```haskell
data Maybe a = Nothing | Just a

instance Functor Maybe where
    fmap _ Nothing = Nothing
    fmap f (Just x) = Just (f x)
```

`Maybe` is an instance of `Functor` and using equational reasoning one can demonstrate that it meets the *functor laws*:
```haskell
-- Identity for the Nothing case
fmap id Nothing
    -- By the definition of fmap
    = Nothing
    -- By the definition of identity
    = id Nothing

-- Identity for the Just case
fmap id (Just x)
    -- By the definition of fmap
    = Just (id x)
    -- By the definition of identity
    = Just x
    -- By the definition of identity
    = id (Just x)

-- Composition for the Nothing case
fmap (f . g) Nothing
    -- By the definition of fmap
    = Nothing
    -- By the definition of fmap
    = fmap f Nothing
    -- By the definition of fmap
    = fmap f (fmap g Nothing)
    -- By the definition of composition
    = (fmap f . fmap g) Nothing

-- Composition for the Just case
fmap (f . g) (Just x)
    -- By the definition of fmap
    = Just ((f . g) x)
    -- By the definition of composition
    = Just (f(g x))
    -- By the definition of fmap
    = fmap f (Just (g x))
    -- By the definition of fmap
    = fmap f (fmap g (Just x))
    -- By the definition of composition
    = (fmap f . fmap g) (Just x)
```

### The Reader Functor
```haskell
instance Functor (( -> ) r) where
    fmap :: (a -> b) -> (r -> a) -> (r -> b)
    fmap f g = f . g
```

`Reader` is the functor that encapsulates function types. The arrow operator (`->`), which is a two argument constructor, is used in prefix form to fix one argument (`r`) and create a type constructor with only one variable type as required by the `Functor` interface.

The following diagram is an interpretation of how a *category of types* might be mapped into *a category of functions* using the `Reader` functor:

![Reader Functor](https://user-images.githubusercontent.com/7043153/121792031-406c5b80-cbbe-11eb-96d2-c6db1a2b2cef.png)

### Functors are Containers
Functors in programming are also considered general-purpose containers. For instance, `Maybe` contains at most 1 element of some given type, `List` can contain zero or more elements of a given type and a `Reader` contains a computation.

### Functors compose
The composition of two functors is a functor whose `fmap` is the composition of the corresponding `fmap`'s:

```haskell
maybeInts :: Maybe [Int]
maybeInts = Just [1, 2, 3]

square :: Int -> Int
square x = x * x

maybeSquares :: Maybe[Int]
maybeSquares = (fmap . fmap) square maybeInts
```


Challenges
----------
#### 1. Can we turn the `Maybe` type constructor into a functor by defining:
```haskell
fmap _ _ = Nothing
```
which ignores both of its arguments? (Hint: Check the functor laws.)

**Answer:** No, we can't because the functor laws won't check for the Just case:
```haskell
-- Identity for the Nothing case
fmap id Nothing
    -- By the definition of fmap
    = Nothing
    -- By the definition of identity
    = id Nothing

-- Identity for the Just case
fmap id (Just x)
    -- By the definition of fmap
    = Nothing
    -- By the definition of fmap
    = fmap id (Just x)
    -- We could go back to Nothing but it's impossible to do something else

-- Composition for the Nothing case
fmap (f . g) Nothing
    -- By the definition of fmap
    = Nothing
    -- By the definition of fmap
    = fmap f Nothing
    -- By the definition of fmap
    = fmap f (fmap g Nothing)
    -- By the definition of composition
    = (fmap f . fmap g) Nothing

-- Composition for the Just case
fmap (f . g) (Just x)
    -- By the definition of fmap
    = Nothing
    -- By the definition of fmap
    = fmap f (Just (g x))
    -- We could go back to Nothing but it's impossible to do something else
```

#### 2. Prove functor laws for the `Reader` functor. Hint: it’s really simple.
```haskell
-- Preservation of identity:
fmap id (r -> a)
    -- By the definition of fmap
    = id . (r -> a)
    -- By the definition of composition
    = id (r -> a)

-- Preservation of composition:
fmap (f . g) (r -> a)
    -- By the definition of fmap
    = (f . g) . (r -> a)
    -- By the definition of composition
    = f(g(r -> a))
    -- By the definition of fmap
    = fmap f (g(r -> a))
    -- By the definition of fmap
    = fmap f (fmap g (r -> a))
    -- By the definition of composition
    = (fmap f . fmap g) (r -> a)
```

#### 3. Implement the `Reader` functor in your second favorite language (the first being Haskell, of course).

See [ReaderTest.java](../java/src/test/java/org/krloxz/training/cattheory/functor/ReaderTest.java)

#### 4. Prove the functor laws for the `List` functor. Assume that the laws are true for the tail part of the list you’re applying it to (in other words, use induction).
```haskell
-- Assumptions:
fmap id xs = id xs
fmap (f . g) xs = (fmap f . fmap g) xs

-- Preservation of identity:
fmap id (x:xs)
    -- By the definition of fmap
    = id x : fmap id xs
    -- By identity on a tail
    = id x : id xs
    -- By the definition of identity
    = x:xs
    -- By the definition of identity
    = id (x:xs)

-- Preservation of composition:
fmap (f . g) (x:xs)
    -- By the definition of fmap
    = (f . g) x : fmap (f . g) xs
    -- By composition on a tail
    = (f . g) x : (fmap f . fmap g) xs
    -- By the definition of composition
    = f(g x) : fmap f (fmap g xs)
    -- By the definition of fmap for 'f'
    = fmap f (g x : fmap g xs)
    -- By the definition of fmap for 'g'
    = fmap f (fmap g (x:xs))
    -- By the definition of composition
    = (fmap f . fmap g) (x:xs)
```
